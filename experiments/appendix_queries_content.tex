%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\subsection{Queries with the LIST data model.}
\label{queries:LIST}

\subsubsection{Operation: first (LIST)}
\label{query:LIST:first}

\begin{lstlisting}[language=sparql]

SELECT
	?event
FROM <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
WHERE {
   <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
        midi:hasEvents ?events .
   ?events rdf:first ?event
}
LIMIT 1

\end{lstlisting}

\subsubsection{Operation: get (LIST)}
\label{query:LIST:get}

\begin{lstlisting}[language=sparql]

SELECT ?event 
FROM <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
WHERE {{
SELECT
	?event (count(?mid) as ?i)
WHERE {
   <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
        midi:hasEvents ?events .
   ?events rdf:rest* ?mid .
   ?mid rdf:rest* ?elt .
   ?elt rdf:first ?event
} 
GROUP BY ?event
}}
ORDER BY ?i
LIMIT 1
OFFSET 23789

\end{lstlisting}

\subsubsection{Operation: rest (LIST)}
\label{query:LIST:rest}

\begin{lstlisting}[language=sparql]

SELECT ?event 
FROM <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
WHERE {
	{{
SELECT
	?event (count(?mid) as ?i)
WHERE {
   <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
        midi:hasEvents ?events .
		?events rdf:rest* ?mid  .
		?mid rdf:rest* ?elt .
		?elt rdf:first ?event .
		filter (?elt != ?events)
} 
GROUP BY ?event
ORDER BY ?i
}}
	}
	
# SKIP THE FIRST ONLY

\end{lstlisting}

\subsubsection{Operation: prev (LIST)}
\label{query:LIST:prev}

\begin{lstlisting}[language=sparql]

SELECT ?event 
FROM <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
WHERE {
	{{
SELECT
	?event (count(?mid) as ?i)
WHERE {
   <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
        midi:hasEvents ?events .
		?events rdf:rest+ ?mid  .
		?mid rdf:rest+ ?elt .
		?elt rdf:first ?event 
} 
GROUP BY ?event
ORDER BY ?i
}}
	}
	
# SKIP THE LAST ONLY

\end{lstlisting}

\subsubsection{Operation: append (LIST)}
\label{query:LIST:append}

\begin{lstlisting}[language=sparql]

WITH <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
DELETE {
	?elt rdf:rest rdf:nil
}
INSERT
	{ ?elt rdf:rest [
		a rdf:List ;
		rdf:first <http://example.org/appended-event> ;
		rdf:rest rdf:nil
		] 
} WHERE	{  
		<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
		midi:hasEvents ?events .
		?events rdf:rest* ?elt .
		?elt rdf:rest rdf:nil
}

\end{lstlisting}

\subsubsection{Operation: append front (LIST)}
\label{query:LIST:append_front}

\begin{lstlisting}[language=sparql]

WITH <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
DELETE {
	<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> midi:hasEvents ?events .
}
INSERT
	{ 
		<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> midi:hasEvents [
			a rdf:List ;
			rdf:first <http://example.org/appended-event> ;
			rdf:rest ?events
		] 
} WHERE	{  
		<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
		midi:hasEvents ?events .
}

\end{lstlisting}

\subsubsection{Operation: popoff (LIST)}
\label{query:LIST:popoff}

\begin{lstlisting}[language=sparql]

WITH <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
DELETE {
	<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> midi:hasEvents ?events .
	?events a rdf:List ;
			rdf:first ?first ;
			rdf:rest ?rest .
}
INSERT
	{ 
		<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> midi:hasEvents ?rest
} WHERE	{  
		<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
		midi:hasEvents ?events .
		?events
			rdf:first ?first ;
			rdf:rest ?rest.
}
# DELETE THE FIRST LIST ELEMENT

\end{lstlisting}

\subsubsection{Operation: set (LIST)}
\label{query:LIST:set}

\begin{lstlisting}[language=sparql]

WITH <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
DELETE
{
  	?elt rdf:first ?event .
}
INSERT {
	?before rdf:first <http://example.org/set-event-23789>
}
WHERE {
  	?elt rdf:first ?event .
	{{
		SELECT ?elt WHERE {{
			SELECT
				?elt (count(?mid) as ?i)
			WHERE {
			   <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
			        midi:hasEvents ?events .
			   ?events rdf:rest* ?mid .
			   ?mid rdf:rest* ?elt .
			} 
			GROUP BY ?elt
		}}
		ORDER BY ?i
		LIMIT 1
		OFFSET 23789
	}}
}


\end{lstlisting}

\subsubsection{Operation: remove at (LIST)}
\label{query:LIST:remove_at}

\begin{lstlisting}[language=sparql]

WITH <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
DELETE
{
  	?elt a rdf:List .
	?elt rdf:first ?first .
	?elt rdf:rest ?after .
	?before rdf:rest ?elt . 
}
INSERT {
	?before rdf:rest ?after
}
WHERE {
	?elt rdf:first ?first .
	OPTIONAL { ?before rdf:rest ?elt } .
	OPTIONAL { ?elt rdf:rest ?after } .
	{{
		SELECT ?elt WHERE {{
			SELECT
				?elt (count(?mid) as ?i)
			WHERE {
			   <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
			        midi:hasEvents ?events .
			   ?events rdf:rest* ?mid .
			   ?mid rdf:rest* ?elt .
			   ?elt rdf:first ?event
			} 
			GROUP BY ?elt
		}}
		ORDER BY ?i
		LIMIT 1
		OFFSET 23789
	}}
}

# VERSION WITH FILTER(?i = 23789) AND NO ORDER BY SAME PERFORMANCE!

\end{lstlisting}


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\subsection{Queries with the PROP NUMBER data model.}
\label{queries:PROP-NUMBER}

\subsubsection{Operation: first (PROP NUMBER)}
\label{query:PROP\-NUMBER:first}

\begin{lstlisting}[language=sparql]

SELECT
	?event
FROM <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
WHERE {
   <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
         midi:hasEvent ?event .
   ?event midi:id ?tick .
} ORDER BY ASC(?tick) LIMIT 1

# GRAPH <data:30k-seq>

\end{lstlisting}

\subsubsection{Operation: get (PROP NUMBER)}
\label{query:PROP\-NUMBER:get}

\begin{lstlisting}[language=sparql]

SELECT
	?event
FROM <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
WHERE {
   <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
         midi:hasEvent ?event .
   ?event midi:id ?tick
} ORDER BY ASC(?tick)
LIMIT 1
OFFSET 23789

\end{lstlisting}

\subsubsection{Operation: rest (PROP NUMBER)}
\label{query:PROP\-NUMBER:rest}

\begin{lstlisting}[language=sparql]

SELECT
	?event
FROM <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
WHERE {
   <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
         midi:hasEvent ?event .
   ?event midi:id ?tick
   FILTER (?tick > 1)
} ORDER BY ?tick

\end{lstlisting}

\subsubsection{Operation: prev (PROP NUMBER)}
\label{query:PROP\-NUMBER:prev}

\begin{lstlisting}[language=sparql]

SELECT
	?event
FROM <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
WHERE {
    <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
          midi:hasEvent ?event .
    ?event midi:id ?tick .
    FILTER (?tick < ?max)
	{{SELECT (MAX(?tick)+1 as ?max) 
		WHERE {
			<http://purl.org/midi-ld/piece/b1e7f60beea64ba369606b2d2fda5d1b/track00> a midi:Track ;
			midi:hasEvent/midi:id ?tick .
		}
	}}
} ORDER BY ?tick

# 23789

\end{lstlisting}

\subsubsection{Operation: append (PROP NUMBER)}
\label{query:PROP\-NUMBER:append}

\begin{lstlisting}[language=sparql]


INSERT
	{ GRAPH <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784> {
  <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> midi:hasEvent <http://example.org/appended-event> .
  <http://example.org/appended-event> midi:id ?next .
   }
  }
WHERE
	{
	{{SELECT (MAX(?tick)+1 as ?next) 
		WHERE {
		GRAPH <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784> {
			<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
			midi:hasEvent/midi:id ?tick .
		}} 
	}}
}


\end{lstlisting}

\subsubsection{Operation: append front (PROP NUMBER)}
\label{query:PROP\-NUMBER:append_front}

\begin{lstlisting}[language=sparql]


WITH <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
DELETE {
	?event midi:id ?oldId
}
INSERT {
    <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> midi:hasEvent <http://example.org/prepended-event> .
		<http://example.org/prepended-event> midi:id 0 .
	?event midi:id ?shifted .
}
WHERE
	{
		BIND ((?oldId + 1) as ?shifted) .
		<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
		midi:hasEvent ?event .
		?event midi:id ?oldId .
	}


\end{lstlisting}

\subsubsection{Operation: popoff (PROP NUMBER)}
\label{query:PROP\-NUMBER:popoff}

\begin{lstlisting}[language=sparql]

WITH <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
DELETE {
	<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> midi:hasEvent ?first .
	?first midi:id 0 .
	?event midi:id ?oldId
}
INSERT {
	?event midi:id ?newId .
}
WHERE {
{{
	SELECT ?event ?oldId ?newId WHERE {
		<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
				midi:hasEvent ?event .
				?event midi:id ?oldId .
		FILTER ( ?oldId > 0 ) .
		BIND ((?oldId-1) AS ?newId)
	}
}}
}


\end{lstlisting}

\subsubsection{Operation: set (PROP NUMBER)}
\label{query:PROP\-NUMBER:set}

\begin{lstlisting}[language=sparql]

WITH <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
DELETE {
	?event midi:id 23789 .
}
INSERT
	{
    <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> midi:hasEvent <http://example.org/inserted-at-position> .
		<http://example.org/inserted-at-position> midi:id 23789 .
	}
WHERE {
    <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
          midi:hasEvent ?event .
		  ?event midi:id 23789 .
}


\end{lstlisting}

\subsubsection{Operation: remove at (PROP NUMBER)}
\label{query:PROP\-NUMBER:remove_at}

\begin{lstlisting}[language=sparql]

WITH <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
DELETE {
	<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> midi:hasEvent ?e .
	?e midi:id 23789 .
	?event midi:id ?oldId
}
INSERT {
	?event midi:id ?newId	
}
WHERE {

{
SELECT ?event ?oldId ?newId WHERE {

	<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
			midi:hasEvent ?event .
			?event midi:id ?oldId .
	FILTER ( ?oldId > 23789 ) .
	BIND ((?oldId-1) AS ?newId)
}
}
}

\end{lstlisting}


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\subsection{Queries with the PROP TIME data model.}
\label{queries:PROP-TIME}

\subsubsection{Operation: first (PROP TIME)}
\label{query:PROP\-TIME:first}

\begin{lstlisting}[language=sparql]

SELECT
	?event
FROM <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
WHERE {
   <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
         midi:hasEvent ?event .
   ?event prov:generatedAtTime ?time .
} ORDER BY ASC(?time) LIMIT 1

# GRAPH <data:30k-seq>

\end{lstlisting}

\subsubsection{Operation: get (PROP TIME)}
\label{query:PROP\-TIME:get}

\begin{lstlisting}[language=sparql]

SELECT
	?event
FROM <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
WHERE {
   <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
         midi:hasEvent ?event .
   ?event prov:generatedAtTime ?time
} ORDER BY ASC(?time)
LIMIT 1
OFFSET 23789

\end{lstlisting}

\subsubsection{Operation: rest (PROP TIME)}
\label{query:PROP\-TIME:rest}

\begin{lstlisting}[language=sparql]

SELECT
	?event
FROM <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
WHERE {
   <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
         midi:hasEvent ?event .
   ?event prov:generatedAtTime ?time
   FILTER (?time > 1)
} ORDER BY ?time

\end{lstlisting}

\subsubsection{Operation: prev (PROP TIME)}
\label{query:PROP\-TIME:prev}

\begin{lstlisting}[language=sparql]

SELECT
	?event
FROM <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
WHERE {
    <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
          midi:hasEvent ?event .
    ?event prov:generatedAtTime ?time .
    FILTER (?time < 23789)
} ORDER BY ?time

# 23789

\end{lstlisting}

\subsubsection{Operation: append (PROP TIME)}
\label{query:PROP\-TIME:append}

\begin{lstlisting}[language=sparql]


INSERT
	{ GRAPH <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784> {
  <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> midi:hasEvent <http://example.org/appended-event> .
  <http://example.org/appended-event> prov:generatedAtTime ?next .
   }
  }
WHERE
	{
	{{SELECT ?events (MAX(?time)+1 as ?next) WHERE {
		GRAPH <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784> {
		<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
			midi:hasEvents ?events .
			?events ?seq ?event .
      ?events prov:generatedAtTime ?time .
		}} GROUP BY ?events
	}}
}

#  GRAPH <data:30k-seq>

\end{lstlisting}

\subsubsection{Operation: append front (PROP TIME)}
\label{query:PROP\-TIME:append_front}

\begin{lstlisting}[language=sparql]


WITH <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
INSERT
	{
    <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> midi:hasEvent <http://example.org/prepended-event> .
		<http://example.org/prepended-event> midi:absoluteTime 0 .
    ?events prov:generatedAtTime ?shifted .
	}
WHERE
	{
		BIND ((?time + 1) as ?shifted) .
		<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
		midi:hasEvents ?events .
		?events prov:generatedAtTime ?time .
	}
#
#  GRAPH <data:30k-seq>
#

\end{lstlisting}

\subsubsection{Operation: popoff (PROP TIME)}
\label{query:PROP\-TIME:popoff}

\begin{lstlisting}[language=sparql]

WITH <http://purl.org/midi-ld/piece/8cf9897535d79e68c33a3076aa06d073>
DELETE {
	<http://purl.org/midi-ld/piece/8cf9897535d79e68c33a3076aa06d073/track00> midi:hasEvent ?first .
}
INSERT
	{
		?event prov:generatedAtTime ?shifted .
	}
WHERE {
{
SELECT ?first ?firstTime ?second ?secondTime ?event ((?time - (?secondTime - ?firstTime)) as ?shifted) WHERE {

GRAPH <http://purl.org/midi-ld/piece/8cf9897535d79e68c33a3076aa06d073> {
		<http://purl.org/midi-ld/piece/8cf9897535d79e68c33a3076aa06d073/track00> a midi:Track ;
			midi:hasEvent ?event .
			?event prov:generatedAtTime ?time .
		}

# First event and its time
{SELECT (?event AS ?first) (MIN(?time) AS ?firstTime) WHERE {
  GRAPH <http://purl.org/midi-ld/piece/8cf9897535d79e68c33a3076aa06d073> {
    <http://purl.org/midi-ld/piece/8cf9897535d79e68c33a3076aa06d073/track00> a midi:Track ;
			midi:hasEvent ?event .
			?event prov:generatedAtTime ?time .
  }
} ORDER BY ASC(?time) LIMIT 1 OFFSET 0 }

# Second event and its time
{SELECT (?event AS ?second) (MIN(?time) AS ?secondTime) WHERE {
  GRAPH <http://purl.org/midi-ld/piece/8cf9897535d79e68c33a3076aa06d073> {
    <http://purl.org/midi-ld/piece/8cf9897535d79e68c33a3076aa06d073/track00> a midi:Track ;
			midi:hasEvent ?event .
			?event prov:generatedAtTime ?time .
  }
} ORDER BY ASC(?time) LIMIT 1 OFFSET 1 }

}
}
}

\end{lstlisting}

\subsubsection{Operation: set (PROP TIME)}
\label{query:PROP\-TIME:set}

\begin{lstlisting}[language=sparql]

WITH <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
DELETE {
	?event midi:absoluteTime 23789 .
}
INSERT
	{
    <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> midi:hasEvent <http://example.org/inserted-at-position> .
		<http://example.org/inserted-at-position> prov:generatedAtTime 23789 .
	}
WHERE {
    <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
          midi:hasEvent ?event .
		  ?event prov:generatedAtTime 23789 .
}

#  GRAPH <data:30k-seq>
# index: 23789

\end{lstlisting}

\subsubsection{Operation: remove at (PROP TIME)}
\label{query:PROP\-TIME:remove_at}

\begin{lstlisting}[language=sparql]

# Element to remove: 0.0

WITH <http://purl.org/midi-ld/piece/8cf9897535d79e68c33a3076aa06d073>
DELETE {
	<http://purl.org/midi-ld/piece/8cf9897535d79e68c33a3076aa06d073/track00> prov:generatedAtTime ?event .
}
WHERE {

{
SELECT ?event WHERE {
  GRAPH <http://purl.org/midi-ld/piece/8cf9897535d79e68c33a3076aa06d073> {
    <http://purl.org/midi-ld/piece/8cf9897535d79e68c33a3076aa06d073/track00> a midi:Track ;
			midi:hasEvent ?event .
			?event prov:generatedAtTime 0.0 .
  }
}
}
}

\end{lstlisting}


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\subsection{Queries with the SEQ data model.}
\label{queries:SEQ}

\subsubsection{Operation: first (SEQ)}
\label{query:SEQ:first}

\begin{lstlisting}[language=sparql]

SELECT
	?event
FROM <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
WHERE {
   <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
         midi:hasEvents [ rdf:_1 ?event ] .
} LIMIT 1

\end{lstlisting}

\subsubsection{Operation: get (SEQ)}
\label{query:SEQ:get}

\begin{lstlisting}[language=sparql]


SELECT
	?event
FROM <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
WHERE {
   <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
         midi:hasEvents [ ?seq ?event ] .
   BIND (xsd:integer(SUBSTR(str(?seq), 45)) AS ?index)
   FILTER (?index = 23789)
} LIMIT 1

\end{lstlisting}

\subsubsection{Operation: rest (SEQ)}
\label{query:SEQ:rest}

\begin{lstlisting}[language=sparql]

SELECT
	?event
FROM <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
WHERE {
   <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
         midi:hasEvents [ ?seq ?event ] .
   BIND (xsd:integer(SUBSTR(str(?seq), 45)) AS ?index)
   FILTER (?index > 1)
} ORDER BY ?index

\end{lstlisting}

\subsubsection{Operation: prev (SEQ)}
\label{query:SEQ:prev}

\begin{lstlisting}[language=sparql]

SELECT
	?event
FROM <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
WHERE {
    <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
          midi:hasEvents [ ?seq ?event ] .
    BIND (xsd:integer(SUBSTR(str(?seq), 45)) AS ?index) .
    FILTER (?index < ?max) .
    {{SELECT (MAX(?pos) as ?max) WHERE {
 	   <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
 	         midi:hasEvents [ ?seq [] ] .
 	   BIND (xsd:integer(SUBSTR(str(?seq), 45)) AS ?pos)
 	}}}
} ORDER BY ?index

\end{lstlisting}

\subsubsection{Operation: append (SEQ)}
\label{query:SEQ:append}

\begin{lstlisting}[language=sparql]

INSERT
	{ GRAPH <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784> { ?events ?property <http://example.org/appended-event> } }
WHERE 
	{  
	BIND (iri(concat("http://www.w3.org/1999/02/22-rdf-syntax-ns#_", str(?next))) as ?property) .
	{{SELECT ?events (MAX(?index)+1 as ?next) WHERE {
		GRAPH <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784> {
		<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
			midi:hasEvents ?events .
			?events ?seq ?event .
			BIND (xsd:integer(SUBSTR(str(?seq), 45)) AS ?index)
		}} GROUP BY ?events
	}}			
}

\end{lstlisting}

\subsubsection{Operation: append front (SEQ)}
\label{query:SEQ:append_front}

\begin{lstlisting}[language=sparql]

WITH <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
DELETE {
	?events ?seq ?event
}
INSERT
	{ 
		?events rdf:_1 <http://example.org/prepended-event> .
		?events ?shifted ?event
	}
WHERE 
	{  
		BIND (iri(concat("http://www.w3.org/1999/02/22-rdf-syntax-ns#_", str(?index + 1))) as ?shifted) .
		<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
		midi:hasEvents ?events .
		?events ?seq ?event .
		BIND (xsd:integer(SUBSTR(str(?seq), 45)) AS ?index)
	}


\end{lstlisting}

\subsubsection{Operation: popoff (SEQ)}
\label{query:SEQ:popoff}

\begin{lstlisting}[language=sparql]

WITH <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
DELETE {
	?events rdf:_1 ?event
}
INSERT
	{ 
		?events ?shifted ?event
	}
WHERE 
	{  
		BIND (iri(concat("http://www.w3.org/1999/02/22-rdf-syntax-ns#_", str(?index - 1))) as ?shifted) .
		<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
		midi:hasEvents ?events .
		?events ?seq ?event .
		BIND (xsd:integer(SUBSTR(str(?seq), 45)) AS ?index) .
		FILTER (?index > 1)
	}

\end{lstlisting}

\subsubsection{Operation: set (SEQ)}
\label{query:SEQ:set}

\begin{lstlisting}[language=sparql]

WITH <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
DELETE {
	?events rdf:_23789 ?event
}
INSERT
	{ 
		?events rdf:_23789 <http://example.org/set-event-23789> .
	}
WHERE {
    <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
          midi:hasEvents ?events . 
		  ?events rdf:_23789 ?event  .
}

\end{lstlisting}

\subsubsection{Operation: remove at (SEQ)}
\label{query:SEQ:remove_at}

\begin{lstlisting}[language=sparql]

WITH <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
DELETE {
	?events rdf:_23789 ?remove
}
INSERT
	{ 
		?events ?shifted ?event
	}
WHERE 
	{  
		BIND (iri(concat("http://www.w3.org/1999/02/22-rdf-syntax-ns#_", str(?index - 1))) as ?shifted) .
		<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
		midi:hasEvents ?events .
		?events ?seq ?event .
		BIND (xsd:integer(SUBSTR(str(?seq), 45)) AS ?index) .
		FILTER ( ?index > 23789 ) .
		{{
			SELECT ?remove WHERE {
				<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
				midi:hasEvents [ rdf:_23789 ?event ].
			}
		}}
	}

\end{lstlisting}


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\subsection{Queries with the SOP data model.}
\label{queries:SOP}

\subsubsection{Operation: first (SOP)}
\label{query:SOP:first}

\begin{lstlisting}[language=sparql]

SELECT
	?event
FROM <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
WHERE {
	<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
		midi:hasEvent ?event .
		FILTER NOT EXISTS { ?event sequence:follows [] }.
} 


\end{lstlisting}

\subsubsection{Operation: get (SOP)}
\label{query:SOP:get}

\begin{lstlisting}[language=sparql]

SELECT
	?event
FROM <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
WHERE {{
SELECT ?event (count(?prev) as ?i)
WHERE {
	<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
		midi:hasEvent ?event .
		?event sequence:follows* ?prev .
} GROUP BY ?event
}}
ORDER BY ?i
LIMIT 1
OFFSET 23789

\end{lstlisting}

\subsubsection{Operation: rest (SOP)}
\label{query:SOP:rest}

\begin{lstlisting}[language=sparql]

SELECT
	?event
FROM <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
WHERE {{
SELECT ?event (count(?prev) as ?i)
WHERE {
	<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
		midi:hasEvent ?event .
		?event sequence:follows+ ?prev .
} GROUP BY ?event
}}
ORDER BY ?i

\end{lstlisting}

\subsubsection{Operation: prev (SOP)}
\label{query:SOP:prev}

\begin{lstlisting}[language=sparql]

SELECT
	?event
FROM <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
WHERE {{
SELECT ?event (count(?prev) as ?i)
WHERE {
	<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
		midi:hasEvent ?event .
		?event sequence:precedes+ ?prev .
} GROUP BY ?event
}}
ORDER BY ?i

\end{lstlisting}

\subsubsection{Operation: append (SOP)}
\label{query:SOP:append}

\begin{lstlisting}[language=sparql]

WITH <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
INSERT
	{ <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00>
		midi:hasEvent <http://example.org/appended-event-SOP> .
		<http://example.org/appended-event-SOP> sequence:follows ?event .
		?event sequence:precedes <http://example.org/appended-event-SOP> .
} WHERE	{  
		<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
		midi:hasEvent ?event .
		FILTER NOT EXISTS {?event sequence:precedes []}
}

\end{lstlisting}

\subsubsection{Operation: append front (SOP)}
\label{query:SOP:append_front}

\begin{lstlisting}[language=sparql]

WITH <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
INSERT
	{ <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00>
		midi:hasEvent <http://example.org/appended-front-event-SOP> .
		<http://example.org/appended-front-event-SOP> sequence:precedes ?event .
		?event sequence:follows <http://example.org/appended-front-event-SOP> .
} WHERE	{  
		<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
		midi:hasEvent ?event .
		FILTER NOT EXISTS {?event sequence:follows []}
}

\end{lstlisting}

\subsubsection{Operation: popoff (SOP)}
\label{query:SOP:popoff}

\begin{lstlisting}[language=sparql]

WITH <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
DELETE {
	<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> midi:hasEvent ?event .
		?event sequence:precedes ?next .
		?next sequence:follows ?event .
} WHERE	{  
		<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
		midi:hasEvent ?event .
		?event sequence:precedes ?next .
		FILTER NOT EXISTS { ?event sequence:follows [] } .
}
# DELETE THE FIRST LIST ELEMENT

\end{lstlisting}

\subsubsection{Operation: set (SOP)}
\label{query:SOP:set}

\begin{lstlisting}[language=sparql]

WITH <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
DELETE
{
	<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> midi:hasEvent ?event .
		?event sequence:precedes ?next .
		?next sequence:follows ?event .
		?prev sequence:precedes ?event .
		?event sequence:follows ?prev .
}
INSERT {
	<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> midi:hasEvent <http://example.org/set-event-23789> .
		<http://example.org/set-event-23789> sequence:precedes ?next .
		?next sequence:follows <http://example.org/set-event-23789> .
		?prev sequence:precedes <http://example.org/set-event-23789> .
		<http://example.org/set-event-23789> sequence:follows ?prev .
}
WHERE {
	<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> midi:hasEvent ?event 
	OPTIONAL { ?event sequence:precedes ?next } .
	OPTIONAL { ?event sequence:follows ?prev } .
	{{
		{{
		SELECT ?event (count(?prev) as ?i)
		WHERE {
			<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
				midi:hasEvent ?event .
				?event sequence:follows* ?prev .
		} 
		GROUP BY ?event
		ORDER BY ?i
		LIMIT 1
		OFFSET 23789
		}}
	}}
}

# VERSION WITH FILTER(?i = 23789) AND NO ORDER BY SAME PERFORMANCE!

\end{lstlisting}

\subsubsection{Operation: remove at (SOP)}
\label{query:SOP:remove_at}

\begin{lstlisting}[language=sparql]

WITH <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
DELETE
{
	<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> midi:hasEvent ?event .
		?event sequence:precedes ?next .
		?next sequence:follows ?event .
		?prev sequence:precedes ?event .
		?event sequence:follows ?prev .
}
INSERT {
	?prev sequence:precedes ?next .
	?next sequence:follows ?prev .
}
WHERE {
	<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> midi:hasEvent ?event 
	OPTIONAL { ?event sequence:precedes ?next } .
	OPTIONAL { ?event sequence:follows ?prev } .
	{{
		{{
		SELECT ?event (count(?prev) as ?i)
		WHERE {
			<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
				midi:hasEvent ?event .
				?event sequence:follows* ?prev .
		} 
		GROUP BY ?event
		ORDER BY ?i
		LIMIT 1
		OFFSET 23789
		}}
	}}
}

# VERSION WITH FILTER(?i = 23789) AND NO ORDER BY SAME PERFORMANCE!

\end{lstlisting}


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\subsection{Queries with the URI data model.}
\label{queries:URI}

\subsubsection{Operation: first (URI)}
\label{query:URI:first}

\begin{lstlisting}[language=sparql]

SELECT
	?event
FROM <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
WHERE {
   <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
         midi:hasEvent ?event .
	     BIND (xsd:integer(SUBSTR(str(?event), 77)) AS ?index)
		 FILTER (?index = 1)
	  } 
LIMIT 1

\end{lstlisting}

\subsubsection{Operation: get (URI)}
\label{query:URI:get}

\begin{lstlisting}[language=sparql]


SELECT
	?event
FROM <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
WHERE {
   <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
         midi:hasEvent ?event .
   BIND (xsd:integer(SUBSTR(str(?event), 77)) AS ?index)
   FILTER (?index = 23789)
} 
LIMIT 1

\end{lstlisting}

\subsubsection{Operation: rest (URI)}
\label{query:URI:rest}

\begin{lstlisting}[language=sparql]

SELECT
	?event
FROM <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
WHERE {
   <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
         midi:hasEvent ?event .
   BIND (xsd:integer(SUBSTR(str(?event), 77)) AS ?index)
   FILTER (?index > 0)
} ORDER BY ?index

\end{lstlisting}

\subsubsection{Operation: prev (URI)}
\label{query:URI:prev}

\begin{lstlisting}[language=sparql]

SELECT
	?event
FROM <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
WHERE {
    <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
          midi:hasEvent ?event .
    BIND (xsd:integer(SUBSTR(str(?event), 77)) AS ?index) .
    FILTER (?index < ?max) .
    {{SELECT (MAX(?pos) as ?max) WHERE {
 	   <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
 	         midi:hasEvent ?event .
 	   BIND (xsd:integer(SUBSTR(str(?event), 77)) AS ?pos)
 	}}}
} ORDER BY ?index

\end{lstlisting}

\subsubsection{Operation: append (URI)}
\label{query:URI:append}

\begin{lstlisting}[language=sparql]

INSERT
	{ GRAPH <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784> { 
		<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> midi:hasEvent ?uri } }
WHERE 
	{  
	BIND (iri(concat("http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00/event", str(?next))) as ?uri) .
	{{SELECT (MAX(?index)+1 as ?next) WHERE {
		GRAPH <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784> {
		<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
			midi:hasEvent ?event .
			BIND (xsd:integer(SUBSTR(str(?event), 77)) AS ?index)
		}} 
	}}			
}



\end{lstlisting}

\subsubsection{Operation: append front (URI)}
\label{query:URI:append_front}

\begin{lstlisting}[language=sparql]

WITH <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
DELETE {
	<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> 
		midi:hasEvent ?olduri .
		?olduri ?p ?o
}
INSERT
	{ 
		<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00>
			 midi:hasEvent <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00/event00000> .
		<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> 
			midi:hasEvent ?newuri .
		?newuri ?p ?o
	}
WHERE 
	{  
		BIND (iri(concat("http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00/event", str(?index + 1))) as ?newuri) .
		<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
		midi:hasEvent ?olduri .
		?olduri ?p ?o .
		BIND (xsd:integer(SUBSTR(str(?olduri), 77)) AS ?index)
	}


\end{lstlisting}

\subsubsection{Operation: popoff (URI)}
\label{query:URI:popoff}

\begin{lstlisting}[language=sparql]

WITH <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
DELETE {
	<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> 
		midi:hasEvent ?popthis ;
		midi:hasEvent ?olduri .
		?popthis ?p1 ?o1 .
		?olduri ?p ?o .
}
INSERT
	{ 
		<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> 
			midi:hasEvent ?newuri .
		?newuri ?p ?o .
	}
WHERE 
	{ 
		BIND (<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00/event0000> as ?popthis) .
		OPTIONAL { ?popthis ?p1 ?o1 }
		OPTIONAL { ?olduri ?p ?o }
		{{
		SELECT ?olduri ?newuri WHERE {
			<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
			midi:hasEvent ?olduri .
			BIND (xsd:integer(SUBSTR(str(?olduri), 77)) AS ?index)
			FILTER (?index > 0) .
			BIND (?index-1 AS ?newindex)
			BIND (
				IF ( STRLEN(str(?newindex)) = 1, CONCAT("000",str(?newindex)), 
					IF ( STRLEN(str(?newindex)) = 2, CONCAT("00",str(?newindex)), 
						IF ( STRLEN(str(?newindex)) = 3, CONCAT("0",str(?newindex)), 
							str(?newindex)
						)
					)
				) AS ?strindex
			)
			BIND (iri(concat("http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00/event", ?strindex)) as ?newuri) .
		}	
		}} .
	}


\end{lstlisting}

\subsubsection{Operation: set (URI)}
\label{query:URI:set}

\begin{lstlisting}[language=sparql]

WITH <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
DELETE {
	<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00>
		midi:hasEvent ?event
}
INSERT
	{ 
		<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00>
			midi:hasEvent <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00/modif23789> .
	}
WHERE {
	{{
		SELECT ?event WHERE {
	    <http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
	          midi:hasEvent ?event .
		      BIND (xsd:integer(SUBSTR(str(?event), 77)) AS ?index) .
		      FILTER (?index = 23789)
		  } LIMIT 1
	}}
}

\end{lstlisting}

\subsubsection{Operation: remove at (URI)}
\label{query:URI:remove_at}

\begin{lstlisting}[language=sparql]

WITH <http://purl.org/midi-ld/piece/2eb43ce7edf27b505bcc0dfb6c283784>
DELETE {
	<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> 
		midi:hasEvent ?popthis ;
		midi:hasEvent ?olduri .
		?popthis ?p1 ?o1 .
		?olduri ?p ?o .
}
INSERT
	{ 
		<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> 
			midi:hasEvent ?newuri .
		?newuri ?p ?o .
	}
WHERE 
	{ 
		BIND (<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00/event23789> as ?popthis)
		OPTIONAL { ?popthis ?p1 ?o1 }
		OPTIONAL { ?olduri ?p ?o }
		{{
		SELECT ?olduri ?newuri WHERE {
			<http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00> a midi:Track ;
			midi:hasEvent ?olduri .
			BIND (xsd:integer(SUBSTR(str(?olduri), 77)) AS ?index)
			FILTER (?index > 23789) .
			
			BIND (?index-1 AS ?newindex)
			BIND (
				IF ( STRLEN(str(?newindex)) = 1, CONCAT("000",str(?newindex)), 
					IF ( STRLEN(str(?newindex)) = 2, CONCAT("00",str(?newindex)), 
						IF ( STRLEN(str(?newindex)) = 3, CONCAT("0",str(?newindex)), 
							str(?newindex)
						)
					)
				) AS ?strindex
			)
			BIND (iri(concat("http://purl.org/midi-ld/piece/2473e18eec6cc55b82c5dddab3bea353/track00/event", ?strindex)) as ?newuri) .
			
		}	
		}} .
	}


\end{lstlisting}


